Import('RTT_ROOT')
Import('rtconfig')
from building import *

cwd = GetCurrentDir()

# add the general drivers.
src = Split("""
delay.c
""")

# add pin drivers.
if GetDepend('RT_USING_PIN'):
    src += ['drv_gpio.c']

# add usart drivers.
if GetDepend(['RT_USING_SERIAL']):
    if GetDepend(['RT_USING_SERIAL_V2']):
        src += ['drv_usart_v2.c']
    else:
        src += ['drv_usart.c']

# add i2c drivers.
if GetDepend(['RT_USING_I2C', 'RT_USING_I2C_BITOPS']):
    if GetDepend('BSP_USING_I2C0') or GetDepend('BSP_USING_I2C1') or GetDepend('BSP_USING_I2C2') or GetDepend('BSP_USING_I2C3'):
        src += ['drv_soft_i2c.c']

# add spi drivers.
if GetDepend('RT_USING_SPI'):
    src += ['drv_spi.c']

# add wdt drivers.
if GetDepend('RT_USING_WDT'):
    src += ['drv_wdt.c']

# add rtc drivers.
if GetDepend('BSP_USING_RTC'):
    src += ['drv_rtc.c']

# add timer drivers.
if GetDepend('RT_USING_HWTIMER'):
    src += ['drv_hwtimer.c']

# add adc drivers.
if GetDepend('RT_USING_ADC'):
    src += ['drv_adc.c']

# add sdio drivers.
if GetDepend('RT_USING_SDIO'):
    src += ['drv_sdio/drv_sdio.c']
    src += ['drv_sdio/drv_sdio_crc.c']

# add pwm drivers.
if GetDepend('RT_USING_PWM'):
    src += ['drv_pwm.c']

# add sdram drivers.
if GetDepend('BSP_USING_SDRAM'):
    src += ['drv_sdram.c']

# add falsh drivers.
if GetDepend(['BSP_USING_ON_CHIP_FLASH']):
    src += ['drv_flash.c']

path = [cwd]
path += [os.path.join(cwd, 'config')]
path += [os.path.join(cwd, 'drv_sdio')]

group = DefineGroup('Drivers', src, depend = [''], CPPPATH = path)

list = os.listdir(cwd)
for item in list:
    if os.path.isfile(os.path.join(cwd, item, 'SConscript')):
        group = group + SConscript(os.path.join(item, 'SConscript'))

Return('group')
